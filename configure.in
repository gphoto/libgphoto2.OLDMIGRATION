dnl Process this file with autoconf to produce a configure script.

AC_INIT(libgphoto2/gphoto2.h)
AM_CONFIG_HEADER(libgphoto2/config.h)
AM_INIT_AUTOMAKE(gphoto2, 2.0beta4dev4)
AM_MAINTAINER_MODE
AC_LIBTOOL_DLOPEN

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CPP
AM_PROG_LIBTOOL
AC_PROG_INSTALL

dnl ---------------------------------------------------------------------------
dnl i18n support (including some hacks)
dnl ---------------------------------------------------------------------------
dnl The following is a hack to get gphoto2 to build on gettext-0.10.35 
dnl systems - BUILD_INCLUDED_LIBINTL is set only in gettext-0.10.37 which is
dnl the gettext version provided with gphoto2.
BUILD_INCLUDED_LIBINTL=no
AC_SUBST(BUILD_INCLUDED_LIBINTL)
dnl USE_INCLUDED_LIBINTL=no
dnl AC_SUBST(USE_INCLUDED_LIBINTL)

ALL_LINGUAS="de"
AM_GNU_GETTEXT

AC_CHECK_FUNC(gettext, gettext_without_libintl=true)
# same trick as with libdl:
# if gettext() doesn't require linking against libintl,
# we don't have to check for gettext in libintl. Otherwise
# we may even require libintl.
dnl AC_CHECK_LIB(intl, gettext, [LIBS="$LIBS -lintl"])
if test "$gettext_without_libintl" != "true" -a "$USE_NLS" = "yes"; then
        AC_CHECK_LIB(intl, gettext, [INTLLIBS="$INTLLIBS -lintl"])
fi

dnl ---------------------------------------------------------------------------
dnl Turn on debugging and all warnings
dnl ---------------------------------------------------------------------------
CFLAGS="$CFLAGS -g -Wall -Wmissing-declarations -Wmissing-prototypes"
LDFLAGS="$LDFLAGS -g -Wall"
AC_SUBST(CFLAGS)
AC_SUBST(LDFLAGS)

dnl Solaris hack for grep and tr
changequote(, )dnl
if test -n "`echo $host_os | grep [sS]olaris`"; then
  TR=/usr/xpg4/bin/tr
  GREP=/usr/xpg4/bin/grep  
else
  TR=tr
  GREP=grep
fi
changequote([, ])dnl

dnl ---------------------------------------------------------------------------
dnl Checks for libraries.
dnl ---------------------------------------------------------------------------
AC_CHECK_FUNC(dlopen, dlopen_without_libdl=true)
# if dlopen doesn't require linking against libdl (e.g. FreeBSD)
# we don't have to check for dlopen in libdl any more
if test "$dlopen_without_libdl" != "true"; then
        AC_CHECK_LIB(dl, dlopen)
fi

dnl Replace `main' with a function in -libs:
AC_CHECK_LIB(ibs, main)

dnl ---------------------------------------------------------------------------
dnl Checks for header files.
dnl ---------------------------------------------------------------------------
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h mcheck.h)

dnl ---------------------------------------------------------------------------
dnl Check for libtool
dnl ---------------------------------------------------------------------------
AC_CHECK_LIB(ltdl, lt_dlforeachfile,
	AC_CHECK_HEADER(ltdl.h,
		AC_DEFINE(HAVE_LTDL)
		LTDL_LIBS="-lltdl"
	)
)
AC_SUBST(LTDL_LIBS)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_CHECK_FUNCS(mkdir strdup strncpy strcpy snprintf sprintf vsnprintf)

dnl Create a stdint.h-like file containing size-specific integer definitions
dnl that will always be available
AC_NEED_STDINT_H(libgphoto2/_stdint.h)

dnl ---------------------------------------------------------------------------
dnl Camera drivers
dnl ---------------------------------------------------------------------------
camlibs='agfa barbie canon casio digita dimera directory jamcam jd11 kodak konica minolta panasonic polaroid ptp samsung sierra stv0680 sonydscf1 sonydscf55'
AC_SUBST(SUBDIRS_CAMLIBS)
for i in $camlibs; do
  d=`echo $i | $TR a-z A-Z`
  eval "CONFIG_DRIVER_$d=n"
done
AC_MSG_CHECKING(which drivers to compile)
AC_ARG_WITH(drivers,
  [  --with-drivers=<list>   compile drivers in <list>; ]
  [                            drivers may be separated with commas; ]
  [                            'all' compiles all drivers; ]
  [                            possible drivers are: ]
  [                            agfa, barbie, canon, casio, digita, dimera,]
  [                            directory, jamcam, jd11, kodak, konica,]
  [                            minolta, panasonic, polaroid, ptp, samsung,]
  [                            sierra, sonydscf1, sonydscf55, stv0680 ],
  drivers="$withval", drivers="all")
if test "$drivers" = "all"; then
  for i in $camlibs; do
    SUBDIRS_CAMLIBS="$SUBDIRS_CAMLIBS $i"
  done
  AC_MSG_RESULT(all)
else
  drivers=`echo $drivers | sed 's/,/ /g'`
  for driver in $drivers; do
    if test -n "`echo $camlibs | $GREP -E \"(^| )$driver( |\$)\"`"; then
      SUBDIRS_CAMLIBS="$SUBDIRS_CAMLIBS $driver"
    else
      AC_ERROR(Unknown driver $driver!)
    fi
  done
  AC_MSG_RESULT($drivers)
fi

dnl ************************************************************
dnl find out where to install documentation files
dnl ************************************************************
GP_CHECK_DOC_DIR()dnl

dnl ********
dnl build docs?
dnl ********
GP_BUILD_DOCS()dnl

dnl ************************************************************
dnl initialize packaging RPMs
dnl ************************************************************
GPKG_CHECK_RPM()dnl

dnl ***
dnl CDK
dnl ***
cdk_msg=no
try_cdk=true
have_cdk=false
AC_ARG_WITH(cdk, [  --without-cdk           Don't use cdk],
	if test x$withval = xno; then
		try_cdk=false
	fi
)
if $try_cdk; then
	AC_CHECK_HEADER(cdk/cdk.h,
		have_cdk=true
		cdk_msg=yes
		AC_DEFINE(HAVE_CDK)
		CDK_LIBS="-lcdk -lncurses"
	)
fi
AM_CONDITIONAL(HAVE_CDK, $have_cdk)
AC_SUBST(CDK_LIBS)

dnl ---------------------------------------------------------------------------
dnl libjpeg: Right now, libjpeg is only used to interpret JPEG previews in
dnl          order to feed them to aalib (see below).
dnl ---------------------------------------------------------------------------
jpeg_msg=no
try_jpeg=true
have_jpeg=false
AC_ARG_WITH(jpeg, [  --without-jpeg          Don't use jpeg],
	if test x$withval = xno; then
		try_jpeg=false
	fi
)
if $try_jpeg; then
	AC_MSG_CHECKING([for jpeglib.h])
	AC_TRY_CPP([
#include <stdio.h>
#undef PACKAGE
#undef VERSION
#undef HAVE_STDDEF_H
#undef HAVE_STDLIB_H
#include <jpeglib.h>],
		jpeg_msg=yes,
		jpeg_msg=no)
	AC_MSG_RESULT($jpeg_msg)
	if test "$jpeg_msg" = yes; then
		have_jpeg=true
		AC_DEFINE(HAVE_JPEG)
		JPEG_LIBS="-ljpeg"
	fi
fi
AM_CONDITIONAL(HAVE_JPEG, $have_jpeg)
AC_SUBST(JPEG_LIBS)

dnl ---------------------------------------------------------------------------
dnl aalib: gphoto2 (the frontend) has been designed as a command-line tool.
dnl	   However, life previews make only sense when you can actually 
dnl        see the preview. This is where aalib fits in - it gives you the
dnl        possibility to view life-previews on any console.
dnl ---------------------------------------------------------------------------
aa_msg=no
try_aa=true
have_aa=false
AC_ARG_WITH(aalib, [  --without-aalib         Don't use aalib],
	if test x$withval = xno; then
		try_aa=false
	fi
)
if $try_aa; then
	AC_CHECK_HEADER(aalib.h,
		have_aa=true
		aa_msg=yes
		AC_DEFINE(HAVE_AA)
		AA_CFLAGS="`aalib-config --cflags`"
		AA_LIBS="`aalib-config --libs || echo ' -laa'`"
	)
fi
AM_CONDITIONAL(HAVE_AA, $have_aa)
AC_SUBST(AA_LIBS)
AC_SUBST(AA_CFLAGS)

dnl ---------------------------------------------------------------------------
dnl Configure libgphoto2-port
dnl ---------------------------------------------------------------------------
AC_CONFIG_SUBDIRS(libgphoto2_port)

AC_OUTPUT([
Makefile
camlibs/Makefile
camlibs/agfa/Makefile
camlibs/barbie/Makefile
camlibs/canon/Makefile
camlibs/canon/doc/Makefile
camlibs/casio/Makefile
camlibs/digita/Makefile
camlibs/dimera/Makefile
camlibs/directory/Makefile
camlibs/jamcam/Makefile
camlibs/jd11/Makefile
camlibs/kodak/Makefile
camlibs/kodak/dc120/Makefile
camlibs/kodak/dc240/Makefile
camlibs/kodak/dc3200/Makefile
camlibs/konica/Makefile
camlibs/konica/localization/Makefile
camlibs/minolta/Makefile
camlibs/minolta/dimagev/Makefile
camlibs/panasonic/Makefile
camlibs/panasonic/l859/Makefile
camlibs/panasonic/coolshot/Makefile
camlibs/polaroid/Makefile
camlibs/ptp/Makefile
camlibs/samsung/Makefile
camlibs/sierra/Makefile
camlibs/sonydscf1/Makefile
camlibs/sonydscf55/Makefile
camlibs/stv0680/Makefile
frontends/Makefile
frontends/command-line/Makefile
tests/Makefile
libgphoto2/Makefile
libgphoto2/libgphoto2.pc
gphoto2-config
intl/Makefile
packaging/Makefile
packaging/rpm/Makefile
packaging/rpm/package.spec
po/Makefile.in
doc/Makefile
doc/api/Makefile
doc/figures/Makefile
])

echo "

Configuration (libgphoto2):

	Source code location:      ${srcdir}
	Compiler:                  ${CC}

	Build documenation:        $enable_gtk_doc
	Config. support (gphoto2): $cdk_msg
	Preview support (gphoto2): $aa_msg
	JPEG support:              $jpeg_msg
"
